# This policy enforces the validation of the allowed regions of GCP resources
# These sentinel policies enforce Wells Fargo security principles

# Import tfplan-functions.sentinel
# with alias "plan"
import "tfplan-functions" as plan
import "strings"
import "types"

prefix = ["us-"]
spanner_region = ["nam10", "nam11", "nam3", "nam6", "nam7", "nam8", "nam9", "regional-us-central1",
	"regional-us-east1", "regional-us-east4", "regional-us-west1", "regional-us-west2",
	"regional-us-west3", "regional-us-west4", "regional-northamerica-northeast1", "regional-northamerica-northeast2"]

cloudsql_allowed_region = ["northamerica-northeast1", "northamerica-northeast2"]

multi_region_var = ["US"]
global_var = ["global"]

# Resource Types Region Map
resourceTypesRegionMap = {
	"google_pubsub_topic": {
		"key": "message_storage_policy.0.allowed_persistence_regions",
	},
	"google_bigquery_dataset": {
		"key": "location",
	},
	"google_dataproc_cluster": {
		"key": "region",
	},
	"google_secret_manager_secret": {
		"key":          "replication.0.user_managed.0.replicas",
		"location_key": "location",
	},
	"google_dialogflow_cx_agent": {
		"key": "location",
	},
	"google_compute_interconnect_attachment": {
		"key": "region",
	},
	"google_spanner_instance": {
		"key": "config",
	},
	"google_sql_database_instance": {
		"key": "region",
	},
	"google_kms_key_ring": {
		"key": "location",
	},
}

# Function to check region prefix OR location prefix
check_for_matches = func(location, address, rc) {
	violations = {}
	for location as lk {
		is_spanner = rule { rc["type"] == "google_spanner_instance" }
		is_dialogflow = rule { rc["type"] == "google_dialogflow_cx_agent" }
		is_cloudsqlInstance = rule { rc["type"] == "google_sql_database_instance" }
		is_kmsKeyring = rule { rc["type"] == "google_kms_key_ring" }

		if is_spanner {

			lk_arr = strings.split(lk, "/")
			lk = lk_arr[-1]

			if not (lk in spanner_region) {
				violations[address] = rc
				print("The location for " + address + " should be in: " + plan.to_string(spanner_region) + "only")
			}

		} else {

			resource_prefix = strings.split(lk, "-")[0] + "-"
			#print(resource_prefix)

			if is_dialogflow {

				if not (resource_prefix in prefix or lk in global_var) {
					violations[address] = rc
					print("The location for " + address + " should be in " + plan.to_string(prefix) + " or " + plan.to_string(global_var) + " region only ")

				}
			} else if is_cloudsqlInstance {
				if not (resource_prefix in prefix or lk in cloudsql_allowed_region) {
					violations[address] = rc
					print("The location for " + address + " should be in " + plan.to_string(prefix) + " or " + plan.to_string(cloudsql_allowed_region) + " region only ")

				}
			} else if is_kmsKeyring {
				if not (resource_prefix in prefix) {
					violations[address] = rc
					print("The location for " + address + " should be in " + plan.to_string(prefix) + " region only ")

				}

			} else {

				if not (resource_prefix in prefix or lk in multi_region_var) {
					violations[address] = rc
					print("The location for " + address + " should be in " + plan.to_string(prefix) + " or " + plan.to_string(multi_region_var) + " region only")
				}
			}
		}

	}
	return violations
}

# Get List Element Type
get_list_element_type = func(list) {
	for list as each_element {
		if types.type_of(each_element) is "string" {
			return "string"
		} else if types.type_of(each_element) is "map" {
			return "map"
		} else {
			return "unknown"
		}
	}
}

# check for Array of map with a specific Key
check_for_location = func(address, rc, location_key_param, location_key) {
	violations = {}
	locations = []
	location = plan.evaluate_attribute(rc.change.after, location_key_param)

	is_undefined = rule { types.type_of(location) is "undefined" }
	if is_undefined {
		violations[address] = rc
	} else {
		is_type = types.type_of(location)
		if is_type is "string" {
			append(locations, location)
		} else if is_type is "list" {
			if get_list_element_type(location) is "string" {
				locations = location
			} else if get_list_element_type(location) is "map" {
				for location as each_map {
					each_location = plan.evaluate_attribute(each_map, location_key)
					append(locations, each_location)
				}
			}
		}
		match = check_for_matches(locations, address, rc)
		violations = match
	}
	return violations
}

allResources = {}
for resourceTypesRegionMap as rt, _ {
	resources = plan.find_resources(rt)
	for resources as address, rc {
		allResources[address] = rc
	}
}

msgs = {}
for allResources as address, rc {
	msg = check_for_location(
		address,
		rc,
		resourceTypesRegionMap[rc["type"]]["key"],
		resourceTypesRegionMap[rc["type"]]["location_key"],
	)
	if length(msg) > 0 {
		msgs[address] = msg
	}
}

GCP_RES_LOCATION = rule { length(msgs) is 0 }

#print(msgs)

# Main rule
main = rule { GCP_RES_LOCATION }
